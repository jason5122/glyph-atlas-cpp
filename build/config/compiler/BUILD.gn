config("default_include_dirs") {
  include_dirs = [
    "//",
    root_gen_dir,
  ]
}

config("compiler") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  cflags_objc = []
  cflags_objcc = []
  ldflags = []
  defines = []
  configs = []

  if (is_mac) {
    configs += [ "//build/config/mac:compiler" ]
  }

  if (is_linux) {
    defines += [ "_GNU_SOURCE" ]
  }

  if (is_win) {
    cflags_c += [ "/std:c17" ]
    cflags_cc += [ "/std:c++20" ]
  } else {
    cflags_c += [ "-std=c17" ]
    cflags_cc += [ "-std=c++20" ]
  }

  # Pass the same C/C++ flags to the Objective-C/C++ compiler.
  cflags_objc += cflags_c
  cflags_objcc += cflags_cc

  if (!is_win) {
    ldflags = [ "-fuse-ld=lld" ]
  }
}

config("no_exceptions") {
  if (is_win) {
    defines = [ "_HAS_EXCEPTIONS=0" ]
  } else {
    cflags_cc = [ "-fno-exceptions" ]
    cflags_objcc = cflags_cc
  }
}

config("c99") {
  if (is_win) {
    # FIXME: Figure out how to override C standard flag on Windows.
    # cflags_c = [ "/std:c99" ]
  } else {
    cflags_c = [ "-std=c99" ]
  }
}

config("optimizations") {
  cflags = [
    "-flto=thin",
    "-O2",
  ]
  ldflags = cflags
}
