config("compiler") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  cflags_objc = []
  cflags_objcc = []
  ldflags = []
  defines = []
  configs = []

  if (is_mac) {
    configs += [ "//build/config/mac:compiler" ]
  }

  if (is_linux) {
    defines += [ "_GNU_SOURCE" ]
  }

  if (is_win) {
    cflags_c += [ "/std:c17" ]
    cflags_cc += [ "/std:c++20" ]
  } else {
    cflags_c += [ "-std=c17" ]
    cflags_cc += [ "-std=c++20" ]

    # TODO: Figure out why we need this.
    cflags_cc += [ "-fexperimental-library" ]
  }

  # Pass the same C/C++ flags to the Objective-C/C++ compiler.
  cflags_objc += cflags_c
  cflags_objcc += cflags_cc

  # if (!is_win) {
  #   ldflags = [ "-fuse-ld=lld" ]
  # }
}

config("default_include_dirs") {
  include_dirs = [
    "//",
    root_gen_dir,
  ]
}

config("optimize") {
  if (is_win) {
    # TODO: Verify these flags do what we want on Windows.
    cflags = [
      "/O2",
      "/Ob2",  # Both explicit and auto inlining.
      "/Oy-",  # Disable omitting frame pointers, must be after /O2.
      "/Zc:inline",  # Remove unreferenced COMDAT (faster links).
      "/Oi",
    ]
  } else {
    cflags = [ "-O2" ]

    # TODO: Consider moving LTO into a separate config.
    ldflags = [ "-flto=thin" ]
  }
}

config("no_optimize") {
  if (is_win) {
    # TODO: Verify these flags do what we want on Windows.
    cflags = [
      "/Od",  # Disable optimization.
      "/Ob0",  # Disable all inlining (on by default).
      "/GF",  # Enable string pooling (off by default).
    ]
  } else {
    cflags = [ "-O0" ]
  }
}

config("default_optimization") {
  if (is_release) {
    configs = [ ":optimize" ]
  } else {
    configs = [ ":no_optimize" ]
  }
}

config("treat_warnings_as_errors") {
  if (is_win) {
    cflags = [ "/WX" ]
  } else {
    cflags = [ "-Werror" ]
    ldflags = [ "-Werror" ]
  }
}

config("default_warnings") {
  cflags = []

  if (is_win) {
    cflags += [ "/W4" ]

    # Opt in to additional [[nodiscard]] on standard library methods.
    defines = [ "_HAS_NODISCARD" ]
  } else {
    cflags += [ "-Wall" ]

    # Clang-specific.
    cflags += [ "-Wextra" ]
  }

  # Disable specific warnings.
  cflags += [
    "-Wno-missing-field-initializers",
    "-Wno-unused-parameter",
  ]

  configs = [ ":treat_warnings_as_errors" ]
}

config("no_exceptions") {
  if (is_win) {
    defines = [ "_HAS_EXCEPTIONS=0" ]
  } else {
    cflags_cc = [ "-fno-exceptions" ]
    cflags_objcc = cflags_cc
  }
}

config("no_rtti") {
  if (is_win) {
    cflags_cc = [ "/GR-" ]
  } else {
    cflags_cc = [ "-fno-rtti" ]
    cflags_objcc = cflags_cc
  }
}

config("c99") {
  if (is_win) {
    # FIXME: Figure out how to override C standard flag on Windows.
    # cflags_c = [ "/std:c99" ]
  } else {
    cflags_c = [ "-std=c99" ]
  }
}
