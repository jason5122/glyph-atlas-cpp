config("default_include_dirs") {
  include_dirs = [
    "//",
    root_gen_dir,
  ]
}

config("compiler") {
  cflags = []
  cflags_c = []
  cflags_cc = []
  cflags_objc = []
  cflags_objcc = []
  ldflags = []
  defines = []
  configs = []

  if (is_mac) {
    configs += [ "//build/config/mac:compiler" ]
  }

  if (is_linux) {
    defines += [ "_GNU_SOURCE" ]
  }

  cflags_c += [ "-std=c17" ]
  cflags_cc += [ "-std=c++17" ]

  # Pass the same C/C++ flags to the Objective-C/C++ compiler.
  cflags_objc += cflags_c
  cflags_objcc += cflags_cc
}

if (is_mac) {
  config("enable_arc") {
    common_flags = [
      "-fobjc-arc",
      "-fno-objc-arc-exceptions",
    ]
    cflags_objc = common_flags
    cflags_objcc = common_flags
  }
}

config("c99") {
  cflags_c = [ "-std=c99" ]
}

config("optimizations") {
  cflags = [
    # "-flto=thin",
    "-O2",
  ]
  ldflags = cflags
}
