import("//build/config/mac/icon.gni")
import("//build/config/mac/mac_app_bundle.gni")

sample_files = [
  "resources/sample_files/emojis.txt",
  "resources/sample_files/proportional_font_test.json",
  "resources/sample_files/sort.scm",
  "resources/sample_files/sort_bugged.scm",
]

tree_sitter_queries = [
  "resources/highlights_cpp.scm",
  "resources/highlights_glsl.scm",
  "resources/highlights_json.scm",
  "resources/highlights_scheme.scm",
]

if (is_mac) {
  mac_app_bundle("app") {
    output_name = "Simple Text"
    bundle_identifier = "com.jason5122.simple-text"
    info_plist = "resources/Info.plist"

    # define `code_signing_identity` in args.gn

    sources = [
      "main.cc",
      "main_mac.mm",
    ]

    frameworks = [ "Cocoa.framework" ]

    deps = [
      ":icon",
      ":sample_files",
      ":tree_sitter_queries",
      "//ui:ui_mac",
    ]
  }

  icon("icon") {
    sources = [ "resources/app.iconset" ]
  }

  bundle_data("sample_files") {
    sources = sample_files
    outputs = [ "{{bundle_resources_dir}}/sample_files/{{source_file_part}}" ]
  }

  bundle_data("tree_sitter_queries") {
    sources = tree_sitter_queries
    outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
  }
}

if (is_linux) {
  executable("app") {
    output_name = "simple_text"

    sources = [
      "main.cc",
      "main_linux.cc",
    ]

    deps = [
      ":sample_files",
      ":tree_sitter_queries",
      "//ui:ui_linux",
    ]
  }

  copy("sample_files") {
    sources = sample_files
    outputs = [ "$root_out_dir/sample_files/{{source_file_part}}" ]
  }

  copy("tree_sitter_queries") {
    sources = tree_sitter_queries
    outputs = [ "$root_out_dir/{{source_file_part}}" ]
  }
}
