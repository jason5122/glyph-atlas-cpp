import("//build/config/mac/icon.gni")
import("//build/config/mac/mac_app_bundle.gni")

if (is_mac) {
  mac_app_bundle("app") {
    output_name = "Simple Text"
    bundle_identifier = "com.jason5122.simple-text"
    info_plist = "resources/Info.plist"

    # define `code_signing_identity` in args.gn

    sources = [
      "AppDelegate.mm",
      "main.cc",
      "main_mac.mm",
    ]

    frameworks = [ "Cocoa.framework" ]

    deps = [
      ":icon",
      ":sample_files",
      ":tree_sitter_queries",
      "//font",
      "//third_party/freetype",
      "//third_party/tree_sitter",
      "//ui",
      "//util",
    ]

    if (is_mac) {
      deps += [ "//ui/renderer:shaders" ]
    }
  }

  icon("icon") {
    sources = [ "resources/app.iconset" ]
  }

  bundle_data("sample_files") {
    sources = [
      "resources/sample_files/10k_lines.json",
      "resources/sample_files/example.json",
      "resources/sample_files/larger_example.json",
      "resources/sample_files/proportional_font_test.json",

      # "resources/sample_files/50k_lines.json",
      # "resources/sample_files/26mb.json",
    ]
    outputs = [ "{{bundle_resources_dir}}/sample_files/{{source_file_part}}" ]
  }

  bundle_data("tree_sitter_queries") {
    sources = [ "resources/highlights.scm" ]
    outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
  }
} else if (is_linux) {
  executable("app") {
    output_name = "simple_text"

    sources = [
      "main.cc",
      "main_linux.c",
    ]

    libs = [
      "wayland-client",
      "wayland-egl",
      "EGL",
      "GL",
    ]

    deps = [ "//third_party/libdecor" ]
  }
}
