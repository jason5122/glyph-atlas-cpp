import("//build/config/mac/icon.gni")
import("//build/config/mac/mac_app_bundle.gni")

if (is_mac) {
  mac_app_bundle("app") {
    output_name = "Simple Text"
    bundle_identifier = "com.jason5122.simple-text"
    info_plist = "resources/Info.plist"

    # define `code_signing_identity` in args.gn

    sources = [
      "main.cc",
      "main_mac.mm",
    ]

    frameworks = [ "Cocoa.framework" ]

    deps = [
      ":icon",
      "//ui/cocoa",
      "//ui/renderer",
    ]
  }

  icon("icon") {
    sources = [ "resources/app.iconset" ]
  }
}

if (is_linux) {
  executable("app") {
    output_name = "simple_text"

    sources = [
      "main.cc",
      "main_linux.cc",
    ]

    deps = [
      "//ui/gtk",
      "//ui/renderer",
    ]
  }
}

if (is_win) {
  executable("app") {
    output_name = "simple_text"

    sources = [
      # "main_win.cc",
      "main.cpp",
    ]

    defines = [
      "UNICODE",
      "_UNICODE",
    ]

    libs = [
      "user32",
      "opengl32",
      "gdi32",
    ]

    deps = [
      "//font",
      "//third_party/libgrapheme",
    ]
  }
}
