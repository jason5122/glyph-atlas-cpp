import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "Simple Text"
  bundle_identifier = "com.jason5122.simple-text"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.mm",
    "controller/WindowController.mm",
    "main.cc",
    "main_mac.mm",
    "model/Atlas.cc",
    "model/AtlasRenderer.cc",
    "model/CursorRenderer.cc",
    "model/Rasterizer.mm",
    "model/Renderer.mm",
    "util/FileUtil.mm",
    "util/LogUtil.mm",
    "view/EditorView.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "QuartzCore.framework",
    "OpenGL.framework",
  ]

  include_dirs = [
    "//app",
    "third_party",

    # TODO: Remove this and automatically include headers using `public` keyword instead
    "third_party/freetype/include",
    "third_party/tree-sitter/include",
    "third_party/tree-sitter-highlight/include",
  ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  cflags = [
    "-flto=thin",
    "-O3",
  ]
  ldflags = cflags

  deps = [
    ":icon",
    ":sample_files",
    ":shaders",
    ":tree_sitter_queries",
    "third_party/freetype:freetype",
    "third_party/tree-sitter:tree-sitter",
  ]
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}

bundle_data("shaders") {
  sources = [
    "resources/shaders/atlas_frag.glsl",
    "resources/shaders/atlas_vert.glsl",
    "resources/shaders/cursor_frag.glsl",
    "resources/shaders/cursor_vert.glsl",
    "resources/shaders/text_frag.glsl",
    "resources/shaders/text_vert.glsl",
  ]
  outputs = [ "{{bundle_resources_dir}}/shaders/{{source_file_part}}" ]
}

bundle_data("sample_files") {
  sources = [
    "resources/sample_files/example.json",
    "resources/sample_files/larger_example.json",
  ]
  outputs = [ "{{bundle_resources_dir}}/sample_files/{{source_file_part}}" ]
}

bundle_data("tree_sitter_queries") {
  sources = [ "resources/highlights.scm" ]
  outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
}
