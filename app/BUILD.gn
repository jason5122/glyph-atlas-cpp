import("//build/config/mac/templates/icon.gni")
import("//build/config/mac/templates/macos_app_bundle.gni")

macos_app_bundle("app") {
  output_name = "GlyphAtlasCpp"
  bundle_identifier = "com.jason5122.glyph-atlas-cpp"
  info_plist = "resources/Info.plist"

  # define `code_signing_identity` in args.gn

  sources = [
    "AppDelegate.m",
    "controller/WindowController.mm",
    "main.m",
    "model/Atlas.cc",
    "model/AtlasRenderer.mm",
    "model/Rasterizer.mm",
    "model/Renderer.mm",
    "util/FileUtil.mm",
    "util/LogUtil.mm",
    "view/OpenGLLayer.mm",
  ]

  frameworks = [
    "Cocoa.framework",
    "QuartzCore.framework",
    "OpenGL.framework",
  ]

  include_dirs = [
    "//app",
    "third_party",

    # TODO: Remove this and automatically include ft2build.h using `public` instead
    "third_party/freetype/include",
  ]

  defines = [ "GL_SILENCE_DEPRECATION" ]

  deps = [
    ":icon",
    ":shaders",
    "third_party/freetype:freetype",
  ]
}

icon("icon") {
  sources = [ "resources/app.iconset" ]
}

bundle_data("shaders") {
  sources = [
    "resources/atlas_frag.glsl",
    "resources/atlas_vert.glsl",
    "resources/example.cc",
    "resources/text_frag.glsl",
    "resources/text_vert.glsl",
  ]
  outputs = [ "{{bundle_resources_dir}}/{{source_file_part}}" ]
}
