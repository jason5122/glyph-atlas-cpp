shared_sources = [
  "renderer/atlas.cc",
  "renderer/rect_renderer.cc",
  "renderer/shader.cc",
  "renderer/text_renderer.cc",
]

if (is_mac) {
  source_set("ui_mac") {
    sources = [
      "mac/EditorView.mm",
      "mac/WindowController.mm",
    ]
    sources += shared_sources

    frameworks = [
      "Cocoa.framework",
      "QuartzCore.framework",
      "OpenGL.framework",
    ]

    defines = [ "GL_SILENCE_DEPRECATION" ]

    deps = [
      "//base",
      "//font",
      "//ui/renderer/shaders",
      "//util",
    ]

    public_deps = [ "//third_party/libgrapheme" ]
  }
}

if (is_linux) {
  source_set("ui_linux") {
    sources = [
      "linux/wayland_client.cc",
      "linux/wayland_window.cc",
    ]
    sources += shared_sources

    libs = [
      "wayland-client",
      "wayland-egl",
      "EGL",
      "GL",
    ]

    public_deps = [
      "//base",
      "//font",
      "//third_party/glad",
      "//third_party/libdecor",
      "//third_party/libgrapheme",
      "//ui/renderer/shaders",
      "//util",
    ]
  }
}
